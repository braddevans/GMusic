import org.apache.tools.ant.filters.*

plugins {
    id 'java-library'
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    maven {
        url = uri('https://repo.papermc.io/repository/maven-public/')
    }

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }
}

group = "dev.geco.gmusic"
version = "1.0.1-dev"
java.sourceCompatibility = JavaVersion.VERSION_17

String pluginName = "GMusic"
String pluginGroup = group.toString()
String pluginVersion = version.toString()
String pluginDescription = "dist"


dependencies {
    implementation("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
    implementation("org.spigotmc:spigot-api:1.21.3-R0.1-SNAPSHOT")
    extraLibs("net.kyori:adventure-api:4.17.0")

    configurations.implementation.extendsFrom(configurations.extraLibs)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        java {
            resources {
                srcDirs = ["src/resources", "src/resource_pack"]
                include '**/**'
            }
        }
    }
}


processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter(ReplaceTokens, tokens: [
                '${pluginName}': pluginName,
                '${pluginGroup}': pluginGroup,
                '${pluginVersion}': pluginVersion,
                '${pluginDescription}': pluginDescription
        ])
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/**/*"
    }
}

